
# fl9 日本語版ドキュメント

**目次**

<!-- START doctoc generated TOC please keep comment here to allow auto update -->
<!-- DON'T EDIT THIS SECTION, INSTEAD RE-RUN doctoc TO UPDATE -->

- [値](#%E5%80%A4)
  - [数値](#%E6%95%B0%E5%80%A4)
  - [文字列](#%E6%96%87%E5%AD%97%E5%88%97)
  - [論理値](#%E8%AB%96%E7%90%86%E5%80%A4)
- [演算子](#%E6%BC%94%E7%AE%97%E5%AD%90)
  - [整数リテラル `123`](#%E6%95%B4%E6%95%B0%E3%83%AA%E3%83%86%E3%83%A9%E3%83%AB-123)
  - [文字列リテラル `'abc'`](#%E6%96%87%E5%AD%97%E5%88%97%E3%83%AA%E3%83%86%E3%83%A9%E3%83%AB-abc)
    - [絶対文字](#%E7%B5%B6%E5%AF%BE%E6%96%87%E5%AD%97)
  - [埋め込み文字列リテラル `"abc"`](#%E5%9F%8B%E3%82%81%E8%BE%BC%E3%81%BF%E6%96%87%E5%AD%97%E5%88%97%E3%83%AA%E3%83%86%E3%83%A9%E3%83%AB-abc)
    - [文字参照](#%E6%96%87%E5%AD%97%E5%8F%82%E7%85%A7)
    - [埋め込み](#%E5%9F%8B%E3%82%81%E8%BE%BC%E3%81%BF)
  - [識別子 `identifier`](#%E8%AD%98%E5%88%A5%E5%AD%90-identifier)
  - [空括弧 `()`](#%E7%A9%BA%E6%8B%AC%E5%BC%A7-)
  - [括弧 `(formula)`](#%E6%8B%AC%E5%BC%A7-formula)
  - [配列初期化子 `[item; ...]`](#%E9%85%8D%E5%88%97%E5%88%9D%E6%9C%9F%E5%8C%96%E5%AD%90-item-)
  - [オブジェクト初期化子 `{key = value; ...}`](#%E3%82%AA%E3%83%96%E3%82%B8%E3%82%A7%E3%82%AF%E3%83%88%E5%88%9D%E6%9C%9F%E5%8C%96%E5%AD%90-key--value-)
  - [ブロック呼び出し `function(closure)`](#%E3%83%96%E3%83%AD%E3%83%83%E3%82%AF%E5%91%BC%E3%81%B3%E5%87%BA%E3%81%97-functionclosure)
  - [関数呼び出し `function[argument; ...]`](#%E9%96%A2%E6%95%B0%E5%91%BC%E3%81%B3%E5%87%BA%E3%81%97-functionargument-)
    - [引数の省略](#%E5%BC%95%E6%95%B0%E3%81%AE%E7%9C%81%E7%95%A5)
    - [名前付き引数 `name : value`](#%E5%90%8D%E5%89%8D%E4%BB%98%E3%81%8D%E5%BC%95%E6%95%B0-name--value)
  - [四則演算 `left + right` `left - right` `left * right` `left / right`](#%E5%9B%9B%E5%89%87%E6%BC%94%E7%AE%97-left--right-left---right-left--right-left--right)
    - [基本](#%E5%9F%BA%E6%9C%AC)
    - [余りの出る除算 `left / right`](#%E4%BD%99%E3%82%8A%E3%81%AE%E5%87%BA%E3%82%8B%E9%99%A4%E7%AE%97-left--right)
    - [文字列結合 `"left" + "right"`](#%E6%96%87%E5%AD%97%E5%88%97%E7%B5%90%E5%90%88-left--right)
    - [文字列の繰り返し `"string" * number`](#%E6%96%87%E5%AD%97%E5%88%97%E3%81%AE%E7%B9%B0%E3%82%8A%E8%BF%94%E3%81%97-string--number)
    - [配列の結合 `[left] + [right]`](#%E9%85%8D%E5%88%97%E3%81%AE%E7%B5%90%E5%90%88-left--right)
    - [配列の繰り返し `[array] * number`](#%E9%85%8D%E5%88%97%E3%81%AE%E7%B9%B0%E3%82%8A%E8%BF%94%E3%81%97-array--number)
    - [型の自動変換](#%E5%9E%8B%E3%81%AE%E8%87%AA%E5%8B%95%E5%A4%89%E6%8F%9B)
    - [オーバーライド](#%E3%82%AA%E3%83%BC%E3%83%90%E3%83%BC%E3%83%A9%E3%82%A4%E3%83%89)
  - [閉じた範囲 `start .. end`](#%E9%96%89%E3%81%98%E3%81%9F%E7%AF%84%E5%9B%B2-start--end)
  - [開いた範囲 `start ~ endExcluded`](#%E9%96%8B%E3%81%84%E3%81%9F%E7%AF%84%E5%9B%B2-start--endexcluded)
  - [ラムダ式 `arguments -> body`](#%E3%83%A9%E3%83%A0%E3%83%80%E5%BC%8F-arguments---body)
    - [引数](#%E5%BC%95%E6%95%B0)
    - [高階関数](#%E9%AB%98%E9%9A%8E%E9%96%A2%E6%95%B0)
  - [変数宣言 `var : val;`](#%E5%A4%89%E6%95%B0%E5%AE%A3%E8%A8%80-var--val)
  - [代入 `var = val;`](#%E4%BB%A3%E5%85%A5-var--val)
  - [パイプ `stream | mapper`](#%E3%83%91%E3%82%A4%E3%83%97-stream--mapper)
  - [文 `line; ...`](#%E6%96%87-line-)
    - [式と文](#%E5%BC%8F%E3%81%A8%E6%96%87)
    - [丸括弧と文](#%E4%B8%B8%E6%8B%AC%E5%BC%A7%E3%81%A8%E6%96%87)
- [高度なチュートリアル](#%E9%AB%98%E5%BA%A6%E3%81%AA%E3%83%81%E3%83%A5%E3%83%BC%E3%83%88%E3%83%AA%E3%82%A2%E3%83%AB)
  - [演算子オーバーライド](#%E6%BC%94%E7%AE%97%E5%AD%90%E3%82%AA%E3%83%BC%E3%83%90%E3%83%BC%E3%83%A9%E3%82%A4%E3%83%89)

<!-- END doctoc generated TOC please keep comment here to allow auto update -->

# 値

## 数値

fl9には数値が1種類だけ存在します。
整数と浮動小数の区別はなく、合わせて数値（number）です。

## 文字列

fl9の文字列は内部的にUnicode形式で格納されています。

## 論理値

TRUEとFALSEの2値が存在します。

# 演算子

## 整数リテラル `123`

整数を書くと整数になります。

```
1
```

```
123456
```

先頭が`0`で始まっても常に10進数です。

```
0010
```
↓
```
10
```

## 文字列リテラル `'abc'`

シングルクォート`'`で囲むと文字列が作れます。

```
'abc'
```

### 絶対文字

`\`は、そのあとに続くいかなる文字もそのまま保持します。
`'`自体を含める場合にも`\'`と書きます。

```
' \' \\ \n '
```
↓
```
 ' \ n 
```

## 埋め込み文字列リテラル `"abc"`

ダブルクォート`"`で囲むと文字列が作れます。

```
"abc"
```

### 文字参照

埋め込み文字列リテラルでは`\`は直後に続く文字に応じて特別な働きをします。

```
" \" \\ \n [\t] \$ "
```
↓
```
 " \ 
 [	] $ 
```

### 埋め込み

埋め込み文字列リテラル内では、`$identifier`によって変数を埋め込むことができます。

```
"[$PI]"
```
↓
```
[3.141592653589793]
```

`$(formula)`で式を埋め込みます。

```
"[$(PI * 100)]"
```
↓
```
[314.1592653589793]
```

## 識別子 `identifier`

識別子は1文字以上で、利用できる文字は`[a-zA-Z0-9_]`です。
ただし先頭の文字に数字を置くことはできません。

fl9にはいくつかの組み込み定数が存在します。

```
PI
```
↓
```
3.141592653589793
```

## 空括弧 `()`

空括弧は要素が0個のストリームを返します。

## 括弧 `(formula)`

丸括弧はただの括弧です。
丸括弧の中には単一の識別子や文字列などの他、式を書くこともできます。

丸括弧はそれで一つの塊になるため、計算順序の変更に使うことができます。

```
10 * (3 + 7)
```
↓
```
100
```

## 配列初期化子 `[item; ...]`

配列初期化子は配列を生成するのに使います。
配列要素には型はなく、入れ子状の配列を含む、どんな値でも入れることができます。

```
[]
```

```
[
  [
    100
  ]
]
```

配列要素は`;`で区切ります。

```
[123; "abc"]
```
↓
```
123,abc
```

`;`を無駄に記述した場合、単に無視されます。

```
[
  ;
  1;
  2;;;;
  3;;;
]
```
↓
```
1,2,3
```

## オブジェクト初期化子 `{key = value; ...}`

オブジェクト初期化子を使ってオブジェクトを生成できます。
`{ }`内部では、`=`で区切られたキー・値の組を`;`で区切って並べます。

配列初期化子と同様、無意味な`;`は単に無視されます。

```
{
  a = 123;
  b = [
    456;
    789;
  ];
}
```
↓
```
a:123;b:456,789;
```

## ブロック呼び出し `function(closure)`

`closure`はコードブロックとして`function`に渡されます。
`function`は常に一つの引数が与えられた状態で呼び出されます。

## 関数呼び出し `function[argument; ...]`

fl9では関数の呼び出しに角括弧`[ ]`を使います。

```
SIN[PI / 6]
```
↓
```
0.49999999999999994
```

関数の引数は`;`で区切ります。

```
LOG[256; 2]
```
↓
```
8
```

### 引数の省略

関数の引数を省略した場合、UNDEFINEDが自動的に挿入されます。

### 名前付き引数 `name : value`

1個以上の名前付き引数が与えられている場合、それらはまとめて一つのオブジェクトとして引数列の最後に挿入されます。

名前付き引数が現れた場合、それ以降は省略してもUNDEFINEDが自動挿入されません。

## 四則演算 `left + right` `left - right` `left * right` `left / right`

### 基本

数値同士の四則演算をサポートしています。

```
106 + 20 * 2 - 8 / 2
```
↓
```
142
```

### 余りの出る除算 `left / right`

除算で余りが出る場合は自動的に浮動小数になります。

```
10 / 3
```
↓
```
3.3333333333333335
```

### 文字列結合 `"left" + "right"`

文字列の加算は、たとえそれが整数と解釈可能な内容であっても、文字列の結合になります。

```
"123" + "456"
```
↓
```
123456
```

### 文字列の繰り返し `"string" * number`

文字列に数値を乗じると、その回数だけ反復します。

```
"123" * 4
```
↓
```
123123123123
```

### 配列の結合 `[left] + [right]`

配列に対して配列を加算すると、それらを結合した配列を新規に生成して返します。

```
[1; 2; 3] + [4; 5; 6]
```
↓
```
1,2,3,4,5,6
```

### 配列の繰り返し `[array] * number`

配列に整数を乗じると、その回数だけ反復します。

```
[1; 2; 3] * 4
```
↓
```
1,2,3,1,2,3,1,2,3,1,2,3
```

### 型の自動変換

右辺の型が左辺によって期待される型でない場合、自動的に型変換が行われます。

例えば、左辺が数値型であれば、右辺が文字列型であっても`+`演算子の挙動は数値の加算となります。

```
123 + "456"
```
↓
```
579
```

### オーバーライド

オブジェクトは四則演算の挙動を次のメソッドで受けます。

- `OPERATOR_ADD`
- `OPERATOR_SUBTRACT`
- `OPERATOR_MULTIPLY`
- `OPERATOR_DIVIDE`

```
obj : {
  (OPERATOR_ADD) = left, right -> 4 + right;
};
obj + 6
```
↓
```
10
```

## 閉じた範囲 `start .. end`

閉じた範囲は左辺から右辺までの整数を1個ずつ返すストリームを生成する演算子です。

```
1 .. 3
```
↓
```
1
2
3
```

## 開いた範囲 `start ~ endExcluded`

開いた範囲は左辺から右辺の手前までの整数を1個ずつ返すストリームを生成する演算子です。

```
1 ~ 3
```
↓
```
1
2
```

この演算子は配列のインデックスの範囲を表すのに便利です。

```
array : [1; 2; 3];
0 ~ $#array
```
↓
```
0
1
2
```

## ラムダ式 `arguments -> body`

無名関数を作る演算子です。
関数は`function[args]`などで呼び出すことができます。

```
f : arg -> arg * 100;
f[7]
```
↓
```
700
```

### 引数

引数は角括弧もしくは丸括弧で囲ってもかまいません。

```
[a] -> a * 100
```

```
(a) -> a * 100
```

引数は0個や複数個でもかまいません。
複数の引数を指定する場合、`;`か`,`で区切って並べてもかまいません。

```
[] -> 100
```

```
() -> 100
```

```
[a; b; c] -> a * b * c
```

```
(a, b, c) -> a * b * c
```

`,`で区切る場合、周囲の括弧を省略できます。

```
f : a, b, c -> a * b * c;
f[2; 5; 7]
```
↓
```
70
```

### 高階関数

関数の引数に関数を与えることもできます。

```
(a -> a[b -> b * 3])[c -> c[4]]
```
↓
```
12
```

## 変数宣言 `var : val;`

丸括弧の中で`:`が現れると、それは変数の宣言です。
変数は`$`などを前置することなく、直接扱うことができます。

```
a : 3;
b : 4;
a * b
```
↓
```
12
```

`:`は丸括弧以外の場所では異なる役割を持つことが多いです。

## 代入 `var = val;`

代入により、変数などの値を変えることができます。
左辺は代入可能なものでなければなりません。

```
a : 3;
a = 7;
a
```
↓
```
7
```

`=`もまた、しばしば代入以外の用途に使われます。

## パイプ `stream | mapper`

`stream`の各要素を`mapper`で変換します。
`mapper`は要素を`_`として受け取るクロージャとして働きます。
fl9では、多くの言語においてmap関数として提供される機能を1文字で行うことができます。

```
1 .. 3 | _ * 100
```
↓
```
100
200
300
```

## 文 `line; ...`

丸括弧の中に現れた`;`は、その左側を文として、処理を順番に実行することを表します。

次の例では、`a`が0から3回1を足されて、最後に10倍されています。
このコードは変数宣言文と代入文と式で構成されています。

```
a : 0;
a = a + 1;
a = a + 1;
a = a + 1;
a = a * 10;
a
```
↓
```
30
```

### 式と文

最後の節のあとには`;`がありません。
これは最後の`a`が、「`a`という式の値を返す」という戻り値の役割をするためです。

最後に式の無い文のみのコードは、voidという特殊な値を返します。
voidは文字列化を含むあらゆる操作が許されません。

```
100;
```
↓
```
Error: Void access
```

式は、その後に`;`を書くことで何も使わずに捨てることができます。
voidに対してはあらゆる操作ができませんが、文にすることで握りつぶすことはできます。
これは副作用のある処理を行うのに適しています。

```
process1[];
(
  process2[];
  process3[];
);
result
```

一部の演算子は式状態と文状態で異なる挙動をします。

### 丸括弧と文

丸括弧の中に文が書けることはこの言語のひとつの特徴です。
この性質により、一部分に複雑な計算が挟まるような式を関数を使わずに書くことができます。

```
1 + 2 + (
  a : 500;
  a = a + 30;
  a
) + 4 + 5
```
↓
```
542
```

# 高度なチュートリアル

## 演算子オーバーライド

fl9ではオブジェクトに特殊なパラメータを付けることで演算子の挙動をオーバーライドできます。

```
four : {
  (OPERATOR_MULTIPLY) = left, right -> 4 * right;
};
four * 25
```
↓
```
100
```

項の値によって挙動を変える演算子の一部は、主軸となる項がオブジェクトだった場合に、そのオブジェクトの特殊なメソッドを呼び出します。
例えば、「加算」であれば、呼び出されるメソッドは`(OPERATOR_MULTIPLY)`です。
`OPERATOR_MULTIPLY`は加算の演算子オーバーライドに使うメソッドを表す組み込み定数です。
この挙動のため、fl9では演算子オーバーロードではなく演算子オーバーライドと呼ばれています。
演算子オーバーライドのメソッドは、必ず第一引数に受け手となったオブジェクト、その他の引数に演算子の他の項が渡されます。
